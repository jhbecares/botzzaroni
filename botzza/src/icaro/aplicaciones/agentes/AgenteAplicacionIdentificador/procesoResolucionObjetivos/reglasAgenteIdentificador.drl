import icaro.infraestructura.recursosOrganizacion.recursoTrazas.ItfUsoRecursoTrazas;
import icaro.infraestructura.recursosOrganizacion.recursoTrazas.imp.componentes.InfoTraza;
import icaro.infraestructura.entidadesBasicas.procesadorCognitivo.*;
import icaro.infraestructura.patronAgenteCognitivo.procesadorObjetivos.gestorTareas.ItfGestorTareas;
import icaro.infraestructura.entidadesBasicas.PerformativaUsuario;
import icaro.infraestructura.entidadesBasicas.comunicacion.*
import icaro.aplicaciones.agentes.AgenteAplicacionIdentificador.objetivos.*;

import icaro.aplicaciones.informacion.gestionQuedadas.*;
import icaro.aplicaciones.agentes.AgenteAplicacionIdentificador.tareas.*;
import icaro.aplicaciones.agentes.AgenteAplicacionIdentificador.tools.*;

global ItfGestorTareas gestorTareas;
global ItfUsoRecursoTrazas recursoTrazas;
global String agentId;


// "grupo" es el nombre del usuario del chat.

rule "Creacion de los objetivos iniciales"
when 
then 
TareaSincrona tarea = gestorTareas.crearTareaSincrona(InicializarInfoWorkMem.class);
    tarea.ejecutar();
	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
end


rule "Saludo Inicial"
when
then
     TareaSincrona tarea = gestorTareas.crearTareaSincrona(SaludoInicial.class);
     tarea.ejecutar(VocabularioGestionQuedadas.IdentRecursoComunicacionChat);
     recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
end

rule "Saludo inicial cuando el grupo ha saludado"
 when
    notif:Notificacion(identInterlc:identNotificador, tipoNotificacion == tipoNotif.saludo)
    not( exists (Grupo(grupo == identInterlc )))
 then
 	Grupo gr = new Grupo();
 	gr.grupo = identInterlc;
 	insert( gr );
 	FocoGrupo fgr = new FocoGrupo(identInterlc);
 	Objetivo ob = new IdentificarGrupo();
 	ob.setobjectReferenceId(identInterlc);
 	insert( fgr );
 	insert( ob );
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    retract(notif);
end


rule "Saludo inicial cuando el grupo dice algo que no es un saludo"
 when
    notif:Notificacion(identInterlc:identNotificador, tipoNotificacion != tipoNotif.saludo)
    not( exists (Grupo(grupo == identInterlc )))
 then
 	Grupo gr = new Grupo();
 	gr.grupo = identInterlc;
 	insert( gr );
 	FocoGrupo fgr = new FocoGrupo(identInterlc);
 	Objetivo ob = new IdentificarGrupo();
 	ob.setobjectReferenceId(identInterlc);
 	insert( fgr );
 	insert( ob );
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(identInterlc,ConversacionGrupo.msg("saludoInicialNoSaludo"));
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    retract(notif);
end


rule "Respuesta ante resaludo del grupo"
 when
    notif:Notificacion(identInterlc:identNotificador, tipoNotificacion == tipoNotif.saludo)
   	gr:Grupo(grupo == identInterlc)
 then
  	 TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
     tarea.ejecutar(identInterlc,ConversacionGrupo.msg("volverASaludar"));
     recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
     gr.actividad();
     retract(notif);
end

rule "Regla de solicitar telefono del grupo"
 when
 	obj:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	fc:FocoGrupo(foco == null, grupo == group)
 	not( exists (ObtenerTelefono(objectReferenceId == group)))
 then
  	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
  	Objetivo ob = new ObtenerTelefono();
  	ob.setobjectReferenceId(group);
  	fc.setFoco(ob);
  	
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("peticionTelefono"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	update(fc);
   	insert(ob);
end

rule "Regla de obtencion del telefono del grupo"
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerTelefono(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion == tipoNotif.telefono, msgg:mensajeNotificacion )
    gr:Grupo(grupo == group)
 then
 	ob.setSolved();
 	gr.setTelefono(msgg);
 	gr.actividad();
 	fc.setFoco(null);
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("prePersistenciaGrupo"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	update(ob);
   	update(fc);
   	retract(notif);
end

rule "Regla de obtencion del telefono del grupo cuando se recibe algo que no es un telefono"
 salience -1
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerTelefono(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion != tipoNotif.telefono, msgg:mensajeNotificacion )
 then
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("noTelefono"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	retract(notif);
end

rule "Regla de solicitar sexo del grupo"
 when
 	obj:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	fc:FocoGrupo(foco == null, grupo == group)
 	not( exists (ObtenerSexo(objectReferenceId == group)))
 then
  	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
  	Objetivo ob = new ObtenerSexo();
  	ob.setobjectReferenceId(group);
  	fc.setFoco(ob);
  	
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("peticionSexo"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	update(fc);
   	insert(ob);
end

rule "Regla de obtencion del sexo del grupo"
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerSexo(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion == tipoNotif.sexo, msgg:mensajeNotificacion )
    gr:Grupo(grupo == group)
 then
 	ob.setSolved();
 	gr.setSexo(msgg);
 	gr.actividad();
 	fc.setFoco(null);
    TareaSincrona tarea1 = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea1.ejecutar(group, "Â¡Genial!");
    TareaSincrona tarea2 = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea2.ejecutar(group,ConversacionGrupo.msg("ultimaPreguntaIdentificacion"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	update(ob);
   	update(fc);
   	retract(notif);
end

rule "Regla de obtencion del sexo del grupo cuando se obtiene algo que no es el sexo"
 salience -1
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerSexo(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion != tipoNotif.sexo, msgg:mensajeNotificacion )
 then
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("noSexo"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	retract(notif);
end

rule "Regla de solicitar edad del grupo"
 when
 	obj:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	fc:FocoGrupo(foco == null, grupo == group)
 	not( exists (ObtenerEdad(objectReferenceId == group)))
 then
  	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
  	Objetivo ob = new ObtenerEdad();
  	ob.setobjectReferenceId(group);
  	fc.setFoco(ob);
  	
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("peticionEdad"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	update(fc);
   	insert(ob);
end

rule "Regla de obtencion de la edad del grupo"
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerEdad(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion == tipoNotif.edad, msgg:mensajeNotificacion )
    gr:Grupo(grupo == group)
 then
 	ob.setSolved();
 	gr.setEdad(Integer.parseInt(msgg));
 	gr.actividad();
 	fc.setFoco(null);
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(ConfirmarEdad.class);
    tarea.ejecutar(group, msgg);
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	update(ob);
   	update(fc);
   	retract(notif);
end

rule "Regla de obtencion de la edad del grupo cuando se recibe algo que no es una edad"
 salience -1
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerEdad(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion != tipoNotif.edad, msgg:mensajeNotificacion )
 then
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("noEdad"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	retract(notif);
end

rule "Regla de solicitar numero de integrantes del grupo"
 when
 	obj:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	fc:FocoGrupo(foco == null, grupo == group)
 	not( exists (ObtenerNumIntegrantes(objectReferenceId == group)))
 then
  	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
  	Objetivo ob = new ObtenerNumIntegrantes();
  	ob.setobjectReferenceId(group);
  	fc.setFoco(ob);
  	
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("peticionNumIntegrantes"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	update(fc);
   	insert(ob);
end

rule "Regla de obtencion del numero de integrantes del grupo"
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerNumIntegrantes(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion == tipoNotif.numintegrantes, msgg:mensajeNotificacion )
    gr:Grupo(grupo == group)
 then
 	ob.setSolved();
 	gr.setNumIntegrantes(Integer.parseInt(msgg));
 	gr.actividad();
 	fc.setFoco(null);
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(ConfirmarNumIntegrantes.class);
    tarea.ejecutar(group, msgg);
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	update(ob);
   	update(gr);
   	update(fc);
   	retract(notif);
end

rule "Regla de obtencion del numero de integrantes cuando no se recibe el num integ"
 salience -1
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerNumIntegrantes(state == Objetivo.PENDING, objectReferenceId == group)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion != tipoNotif.numintegrantes, msgg:mensajeNotificacion )
 then
 	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(group,ConversacionGrupo.msg("noNumIntegrantes"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
	
   	retract(notif);
end


rule "Regla de obtencion de la contrasena del grupo (telefono) cuando no se obtiene un telefono"
 salience -1
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.REFINED)
 	ob:ObtenerIDGrupo(objectReferenceId == group, state == Objetivo.SOLVED)
 	fc:FocoGrupo(grupo == group)
    notif:Notificacion(identNotificador == group, tipoNotificacion != tipoNotif.telefono, msgg:mensajeNotificacion )
 then
 	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
  	tarea.ejecutar(group, ConversacionGrupo.msg("noPass"));
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
	
   	retract(notif);
end

rule "Regla de solicitud del identificador del grupo"
 when
 	obj:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	fc:FocoGrupo(foco == null, grupo == group)
 	not( exists (ObtenerIDGrupo(objectReferenceId == group)))
 then
  	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
  	
  	Objetivo ob = new ObtenerIDGrupo();
	ob.setobjectReferenceId(group);
	fc.setFoco(ob);
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
  	tarea.ejecutar(group, ConversacionGrupo.msg("peticionIDGrupo"));	
  
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
    insert(ob);
    update(obj);
    update(fc);
end


rule "Regla de obtencion del identificador del grupo cuando no se recibe un identificador"
 salience -1
 when
 	obGeneral:IdentificarGrupo(group:objectReferenceId, state == Objetivo.PENDING)
 	ob:ObtenerIDGrupo(objectReferenceId == group, state == Objetivo.PENDING)
 	fc:FocoGrupo(grupo == group, foco == ob )
    notif:Notificacion(identNotificador == group, tipoNotificacion != tipoNotif.idgrupo, msgg:mensajeNotificacion )
 then
 	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
  	tarea.ejecutar(group, ConversacionGrupo.msg("noIDGrupo"));	
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
	
   	retract(notif);
end

rule "Notificar desconocimiento"
 when
 	notif:Notificacion(group:identNotificador, tipoNotificacion == VocabularioGestionQuedadas.ExtraccionSemanticaNull )
 	fc:FocoGrupo(grupo == group)
 then
 	TareaSincrona tarea = gestorTareas.crearTareaSincrona(NotificarAlGrupoSinContexto.class);
    tarea.ejecutar(fc);
    retract(notif);
    update(fc);
 	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
end


rule "Despedirse"
	when 
	 notif:Notificacion(identInterlc:identNotificador, tipoNotificacion == tipoNotif.despedida)
 	 gr:Grupo(grupo == identInterlc )
	 then
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
   Tarea tarea = gestorTareas.crearTarea(EliminarSesionGrupo.class);
     tarea.ejecutar(gr.grupo);
   TareaSincrona tarea2 = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
     tarea2.ejecutar(gr.grupo,  ConversacionGrupo.msg("despedida"));
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   
   retract( notif );
end

